import org.gradle.internal.os.OperatingSystem

allprojects  {
	apply plugin: 'java-library'
	group = 'com.ardor3d'
	version = '1.6.2'
    project.ext.lwjglVersion = "3.3.3"
    
	switch ( OperatingSystem.current() ) {
		case OperatingSystem.WINDOWS:
			project.ext.lwjglNatives = "natives-windows"
			break
		case OperatingSystem.LINUX:
			project.ext.lwjglNatives = "natives-linux"
			break
		case OperatingSystem.MAC_OS:
			project.ext.lwjglNatives = "natives-macos"
			break
	}    
}

subprojects {
	apply plugin: 'java-library'
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	tasks.withType(JavaCompile).configureEach {
		options.encoding = 'UTF-8'
	}

	tasks.register('packageSources', Jar) {
		from sourceSets.main.allSource
	}
	
	artifacts.archives packageSources
	repositories {
    	mavenCentral()
		maven { url "https://repo.maven.apache.org/maven2" }
	}

	configurations.configureEach {
		resolutionStrategy {
	        dependencySubstitution {
	            // The maven property ${osgi.platform} is not handled by Gradle
	            // so we replace the dependency, using the osgi platform from the project settings
				def os = System.getProperty("os.name").toLowerCase()
				if (os.contains("windows")) {
		            substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.win32.win32.x86_64:3.108.0")
				}
				else if (os.contains("linux")) {
		            substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.gtk.linux.x86_64:3.108.0")
				}
				else if (os.contains("mac")) {
		            substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') with module("org.eclipse.platform:org.eclipse.swt.cocoa.macosx.x86_64:3.108.0")
				}
	        }
	    }
	}
	
	dependencies {
		testImplementation group: 'junit', name: 'junit', version:'4.13.2'
		testImplementation group: 'org.easymock', name: 'easymock', version: '4.3'
	}
}
